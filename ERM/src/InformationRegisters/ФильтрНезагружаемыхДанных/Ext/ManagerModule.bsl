#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьТаблицуФильтра(Source) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФильтрНезагружаемыхДанных.Company,
		|	ФильтрНезагружаемыхДанных.AU
		|ИЗ
		|	РегистрСведений.ФильтрНезагружаемыхДанных КАК ФильтрНезагружаемыхДанных
		|ГДЕ
		|	ФильтрНезагружаемыхДанных.Source = &Source";
	
	Запрос.УстановитьПараметр("Source", Source);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаФильтра = РезультатЗапроса.Выгрузить();
	ТаблицаФильтра.Индексы.Добавить("Company, AU");
	ТаблицаФильтра.Индексы.Добавить("AU");
	
	Возврат ТаблицаФильтра;
	
КонецФункции

Функция ЭтоНезагружаемыеДанные(ТаблицаФильтра, СтрокаДанных) Экспорт
	
	Если ТаблицаФильтра.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураПоискаCompanyAU = Новый Структура("Company, AU");
	СтруктураПоискаCompanyAU.Company = СтрокаДанных.Company;
	СтруктураПоискаCompanyAU.AU = СтрокаДанных.AU;
	
	Если ТаблицаФильтра.НайтиСтроки(СтруктураПоискаCompanyAU).Количество() > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтруктураПоискаCompanyAU.Company = Справочники.Организации.ПустаяСсылка();
	Если ТаблицаФильтра.НайтиСтроки(СтруктураПоискаCompanyAU).Количество() > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтруктураПоискаCompanyAU.Company = СтрокаДанных.Company;
	СтруктураПоискаCompanyAU.AU = Справочники.КостЦентры.ПустаяСсылка();
	Если ТаблицаФильтра.НайтиСтроки(СтруктураПоискаCompanyAU).Количество() > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецЕсли
