
Процедура ПриЗаписи(Отказ, Замещение)
	
	МассивСО = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЭтотОбъект.ВыгрузитьКолонку("SalesOrder"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	SalesOrderResponsibles.SalesOrder КАК SalesOrder,
		|	SalesOrderResponsibles.ResponsibleAR.Представление КАК ResponsibleARПредставление
		|ИЗ
		|	РегистрСведений.SalesOrderResponsibles КАК SalesOrderResponsibles
		|ГДЕ
		|	SalesOrderResponsibles.SalesOrder В(&МассивСО)
		|ИТОГИ ПО
		|	SalesOrder";
	
	Запрос.УстановитьПараметр("МассивСО", МассивСО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаSalesOrder = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НЗ = РегистрыСведений.SalesOrderResponsiblesList.СоздатьНаборЗаписей();
	МассивПредставлений = Новый Массив;
	
	Пока ВыборкаSalesOrder.Следующий() Цикл
		
		НЗ.Очистить();
		НЗ.Отбор.SalesOrder.Установить(ВыборкаSalesOrder.SalesOrder);
		МассивПредставлений.Очистить();
		
		ВыборкаДетальныеЗаписи = ВыборкаSalesOrder.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МассивПредставлений.Добавить(СокрЛП(ВыборкаДетальныеЗаписи.ResponsibleARПредставление));
		КонецЦикла;
		
		СтрокаНабора = НЗ.Добавить();
		СтрокаНабора.SalesOrder = ВыборкаSalesOrder.SalesOrder;
		СтрокаНабора.ResponsiblesList = СтрСоединить(МассивПредставлений, ", ");
		НЗ.Записать(Истина);
		
	КонецЦикла;
	
	
КонецПроцедуры
