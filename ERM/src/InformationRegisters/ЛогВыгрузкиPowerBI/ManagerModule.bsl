#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗалогироватьСобытие(ПараметрыЛогирования) Экспорт
	
	Если НЕ ПараметрыЛогирования.Свойство("ДатаСобытия") ИЛИ Не ЗначениеЗаполнено(ПараметрыЛогирования.ДатаСобытия) Тогда
		ПараметрыЛогирования.Вставить("ДатаСобытия", ТекущаяДата());
	КонецЕсли;
	
	Если Не ПараметрыЛогирования.Свойство("ПорядковыйНомерСобытия") Тогда
		ПараметрыЛогирования.Вставить("ПорядковыйНомерСобытия", 0);
	Иначе
		ПараметрыЛогирования.ПорядковыйНомерСобытия = ПараметрыЛогирования.ПорядковыйНомерСобытия + 1;
	КонецЕсли;
	
	Если Не ПараметрыЛогирования.Свойство("НомерСеанса") ИЛИ Не ЗначениеЗаполнено(ПараметрыЛогирования.НомерСеанса) Тогда
		ПараметрыЛогирования.Вставить("НомерСеанса", НомерСеансаИнформационнойБазы());
	КонецЕсли;
	
	Если Не ПараметрыЛогирования.Свойство("Пользователь") ИЛИ Не ЗначениеЗаполнено(ПараметрыЛогирования.Пользователь) Тогда
		ПараметрыЛогирования.Вставить("Пользователь", Пользователи.ТекущийПользователь());
	КонецЕсли;
	
	ДобавитьЗапись(ПараметрыЛогирования);
	
КонецПроцедуры

Функция НовыеПараметрыЛогирования(ИнициализироватьИдентификаторСеанса = Истина) Экспорт
	
	ПараметрыЛогирования = Новый Структура("ДатаСобытия, ПорядковыйНомерСобытия, ИдентификаторСеанса, 
		|Источник, Пользователь, НомерСеанса, Событие, Операция, Комментарий, ЭтоОшибка");
	Если ИнициализироватьИдентификаторСеанса Тогда
		ПараметрыЛогирования.ИдентификаторСеанса = Новый УникальныйИдентификатор();
	КонецЕсли;
	ПараметрыЛогирования.ПорядковыйНомерСобытия = 0;
	
	Возврат ПараметрыЛогирования;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "ЛогВыгрузкиPowerBI", Загрузка);
	
КонецПроцедуры

// Процедура удаляет набор записей в регистре по переданным значениям структуры.
Процедура УдалитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, "ЛогВыгрузкиPowerBI", Загрузка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли