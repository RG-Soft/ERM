#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПерваяСтрокаДанных = 2;
	ИменаКолонокВПервойСтроке = Истина;

	
	СтруктураКолонок.Очистить();
	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "UID";
	СтрокаТЗ.ИмяКолонки = "UID";
	СтрокаТЗ.НомерКолонки = 1;

	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "CustomerDesc";
	СтрокаТЗ.ИмяКолонки = "CustomerDesc";
	СтрокаТЗ.НомерКолонки = 2;
	
	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "CRM";
	СтрокаТЗ.ИмяКолонки = "CRMID";
	СтрокаТЗ.НомерКолонки = 3;
	
	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "HOBs";
	СтрокаТЗ.ИмяКолонки = "INN";
	СтрокаТЗ.НомерКолонки = 4;
	
	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "OracleSmith";
	СтрокаТЗ.ИмяКолонки = "SmithID";
	СтрокаТЗ.НомерКолонки = 5;
	
	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "OracleMI";
	СтрокаТЗ.ИмяКолонки = "MiID";
	СтрокаТЗ.НомерКолонки = 6;
	
	СтрокаТЗ = СтруктураКолонок.Добавить();
	СтрокаТЗ.ИмяПоля = "Lawson";
	СтрокаТЗ.ИмяКолонки = "Lawson_ID";
	СтрокаТЗ.НомерКолонки = 7;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	АдресВХранилище = "";
	ВыбранноеИмяФайла = "";
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ИмяФайлаНачалоВыбораЗавершение", ЭтотОбъект);
	
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, АдресВХранилище,,, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбораЗавершение(Результат, АдресВХранилище, ВыбранноеИмяФайла, ДополнительныеПараметры)Экспорт
	
	Если Результат Тогда
		
		Файл = Новый Файл(ВыбранноеИмяФайла);
		
		ИмяФайла = Файл.Имя;
		АдресФайлаВХранилище = АдресВХранилище;
		Модифицированность = Истина;
		
		ЗаполнитьСписокЛистовЭкселя(Истина);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНумерациюКолонокПриИзменении(Элемент)
	Элементы.СтруктураКолонок.Видимость = ПоказыватьНумерациюКолонок;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрочитатьФайл(Команда)
	ПрочитатьФайлНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВРегистр(Команда)
	ДобавитьВРегистрНаСервере();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьФайлНаСервере()
	
	ТаблицаДанных.Очистить();
	
	СтруктураПараметров = Новый Структура("ЛистФайла, ИменаКолонокВПервойСтроке, ПерваяСтрокаДанных, ПоследняяСтрокаДанных");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ЭтотОбъект);
	СтруктураПараметров.Вставить("СтруктураКолонок", РеквизитФормыВЗначение("СтруктураКолонок"));
	СтруктураПараметров.Вставить("ТаблицаДанных", РеквизитФормыВЗначение("ТаблицаДанных"));
	СтруктураПараметров.Вставить("ЛистФайла", ЛистФайла);
	
	ДанныеДляЗаполнения = Новый Структура(); 
	ТекстОшибки = "";
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла("xlsx");
	ФайлЭксель = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
	ФайлЭксель.Записать(ПутьКФайлу);
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	rgsОбщегоНазначения.ВыгрузитьЭксельВТаблицуДанных(ПутьКФайлу, ТаблицаДанных, ДанныеДляЗаполнения, АдресХранилища, СтруктураПараметров);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЛистовЭкселя(ЗаполнитьЛист = Ложь)
	
	СписокЛистов = Новый Массив;
	
	Если ЭтоАдресВременногоХранилища(АдресФайлаВХранилище) Тогда
		ФайлЭксель = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
	Иначе
		ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
		ФайлЭксель = ОбъектДляСервера.ИсточникДанных.Получить();
	КонецЕсли;
	
	Если ФайлЭксель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла("xlsx");
	ФайлЭксель.Записать(ПутьКФайлу);
	
	Connection = Новый COMОбъект("ADODB.Connection");
	СтрокаПодключения = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" + СокрЛП(ПутьКФайлу) + ";Extended Properties=""Excel 12.0 Xml;HDR=" + ?(ИменаКолонокВПервойСтроке, "Yes", "No") + """";
	Попытка
		Connection.Open(СтрокаПодключения);
	Исключение
		Попытка
			СтрокаПодключения = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" + СокрЛП(ПутьКФайлу) + ";Extended Properties=""Excel 8.0;HDR=" + ?(ИменаКолонокВПервойСтроке, "Yes", "No") + """";
			Connection.Open(СтрокаПодключения);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецПопытки;
	
	rs = Новый COMObject("ADODB.RecordSet");
	rs.ActiveConnection = Connection;
	rs = Connection.OpenSchema(20);
	
	Пока rs.EOF() = 0 Цикл
		Если Найти(rs.Fields("TABLE_NAME").Value, "_FilterDatabase") = 0 Тогда
			СписокЛистов.Добавить(rs.Fields("TABLE_NAME").Value);
		КонецЕсли;
		rs.MoveNext();
	КонецЦикла;
	
	rs.Close();
	Connection.Close();
	
	Элементы.ЛистФайла.СписокВыбора.ЗагрузитьЗначения(СписокЛистов);
	
	Если СписокЛистов.Количество() > 0 И ЗаполнитьЛист Тогда
		ЛистФайла = СписокЛистов[0];
	Конецесли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВРегистрНаСервере()
	
	Колонки = Новый Массив;
	Колонки.Добавить("CRM");
	Колонки.Добавить("HOBs");
	Колонки.Добавить("OracleSmith");
	Колонки.Добавить("OracleMI");
	Колонки.Добавить("Lawson");
	
	ТаблицаНеПустыхВариантов = Новый ТаблицаЗначений;
	ТаблицаНеПустыхВариантов.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов ("Строка"));
	ТаблицаНеПустыхВариантов.Колонки.Добавить("ТипСоответствия", Новый ОписаниеТипов ("ПеречислениеСсылка.ТипыСоответствий"));
	ТаблицаНеПустыхВариантов.Колонки.Добавить("ТипОбъектаВнешнейСистемы", Новый ОписаниеТипов ("ПеречислениеСсылка.ТипыОбъектовВнешнихСистем"));
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		Для Каждого Колонка Из Колонки Цикл
			Если СтрокаТаблицы[Колонка] <> "" Тогда				
				ТекущаяСтрокаТаблицыНеПустыхЗначений = ТаблицаНеПустыхВариантов.Добавить();
				ТекущаяСтрокаТаблицыНеПустыхЗначений.ТипСоответствия = Перечисления.ТипыСоответствий[Колонка];
				ТекущаяСтрокаТаблицыНеПустыхЗначений.ТипОбъектаВнешнейСистемы = Перечисления.ТипыОбъектовВнешнихСистем.Client;
				ТекущаяСтрокаТаблицыНеПустыхЗначений.Идентификатор = СтрокаТаблицы[Колонка];
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ТаблицаНеПустыхВариантов.Идентификатор КАК СТРОКА(100)) КАК Идентификатор,
	               |	ТаблицаНеПустыхВариантов.ТипСоответствия,
	               |	ТаблицаНеПустыхВариантов.ТипОбъектаВнешнейСистемы
	               |ПОМЕСТИТЬ ТаблицаНеПустыхВариантов
	               |ИЗ
	               |	&ТаблицаНеПустыхВариантов КАК ТаблицаНеПустыхВариантов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.ОбъектПриемника,
	               |	НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.Идентификатор,
	               |	НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.ТипСоответствия,
	               |	НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.ТипОбъектаВнешнейСистемы
	               |ИЗ
	               |	ТаблицаНеПустыхВариантов КАК ТаблицаНеПустыхВариантов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаСинхронизацииОбъектовСВнешнимиСистемами.СрезПоследних КАК НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних
	               |		ПО ТаблицаНеПустыхВариантов.ТипСоответствия = НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.ТипСоответствия
	               |			И ТаблицаНеПустыхВариантов.ТипОбъектаВнешнейСистемы = НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.ТипОбъектаВнешнейСистемы
	               |			И ТаблицаНеПустыхВариантов.Идентификатор = НастройкаСинхронизацииОбъектовСВнешнимиСистемамиСрезПоследних.Идентификатор";
	Запрос.УстановитьПараметр("ТаблицаНеПустыхВариантов", ТаблицаНеПустыхВариантов);
	ВыборкаНеПустыхВариантов = Запрос.Выполнить().Выбрать();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		МассивНеПустыхКолонок = Новый Массив;
		Для Каждого Колонка Из Колонки Цикл
			Если СтрокаТаблицы[Колонка] <> "" Тогда
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Идентификатор", СтрокаТаблицы[Колонка]);
				СтруктураПоиска.Вставить("ТипОбъектаВнешнейСистемы", Перечисления.ТипыОбъектовВнешнихСистем.Client);
				СтруктураПоиска.Вставить("ТипСоответствия", Перечисления.ТипыСоответствий[Колонка]);
				ВыборкаНеПустыхВариантов.Сбросить();
				ОбъектСуществует = ВыборкаНеПустыхВариантов.НайтиСледующий(СтруктураПоиска);
				Если ОбъектСуществует Тогда 
					СуществующийКонтрагент = ВыборкаНеПустыхВариантов.ОбъектПриемника;
				Иначе
					МассивНеПустыхКолонок.Добавить(Колонка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Контрагент = ?(ОбъектСуществует, СуществующийКонтрагент, СтрокаТаблицы.CustomerDesc);
		
		Если Не ЗначениеЗаполнено(Справочники.Контрагенты.НайтиПоНаименованию(Контрагент)) Тогда 
			КонтрагентЗапись = Справочники.Контрагенты.СоздатьЭлемент();
			КонтрагентЗапись.Наименование = СтрокаТаблицы.CustomerDesc;
			КонтрагентЗапись.ОбменДанными.Загрузка = Истина;
			КонтрагентЗапись.Записать();
		КонецЕсли;
		
		Для Каждого Элемент Из МассивНеПустыхКолонок Цикл;
			Запись = РегистрыСведений.НастройкаСинхронизацииОбъектовСВнешнимиСистемами.СоздатьМенеджерЗаписи();
			Запись.Идентификатор = СтрокаТаблицы[Элемент];
			Запись.ОбъектПриемника = Справочники.Контрагенты.НайтиПоНаименованию(Контрагент);
			Запись.ТипОбъектаВнешнейСистемы = Перечисления.ТипыОбъектовВнешнихСистем.Client;
			Запись.ТипСоответствия = Перечисления.ТипыСоответствий[Элемент];
			Запись.Период = ТекущаяДата();
			Запись.Записать();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
