#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция НайтиТранзакцииLawson(Дата, ВидСчета, ManagementGeomarket) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док_ПроводкаDSS.Ссылка КАК Транзакция,
		|	Док_ПроводкаDSS.TranAmount КАК Amount,
		|	Док_ПроводкаDSS.КонтрагентLawson КАК Client,
		|	ВЫБОР
		|		КОГДА ВнутренниеКурсыВалютСрезПоследних.Курс ЕСТЬ NULL
		|				ИЛИ ВнутренниеКурсыВалютСрезПоследних.Курс = 0
		|			ТОГДА 0
		|		ИНАЧЕ Док_ПроводкаDSS.TranAmount / ВнутренниеКурсыВалютСрезПоследних.Курс
		|	КОНЕЦ КАК BaseAmount,
		|	Док_ПроводкаDSS.Currency КАК Currency,
		|	Док_ПроводкаDSS.AU.ПодразделениеОрганизации.БазовыйЭлемент.GeoMarket.ManagementGeomarket КАК ManagementGeomarket,
		|	ВЫБОР
		|		КОГДА НЕ СоответсвиеКорректировкиРегистровПроводки.КорректировкаРегистров.Ссылка ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьКорректировка,
		|	КорректировкаТранзакции.Ссылка КАК КорректировкаТранзакции
		|ИЗ
		|	Документ.ПроводкаDSS КАК Док_ПроводкаDSS
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВнутренниеКурсыВалют.СрезПоследних(&КонецПериода, ) КАК ВнутренниеКурсыВалютСрезПоследних
		|		ПО Док_ПроводкаDSS.Currency = ВнутренниеКурсыВалютСрезПоследних.Валюта
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответсвиеКорректировкиРегистровПроводки КАК СоответсвиеКорректировкиРегистровПроводки
		|		ПО Док_ПроводкаDSS.Ссылка = СоответсвиеКорректировкиРегистровПроводки.Транзакция
		|			И (НЕ СоответсвиеКорректировкиРегистровПроводки.Inactive)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаТранзакции КАК КорректировкаТранзакции
		|		ПО Док_ПроводкаDSS.Ссылка = КорректировкаТранзакции.ДокументОснование
		|			И (КорректировкаТранзакции.Проведен)
		|ГДЕ
		|	(Док_ПроводкаDSS.AccountLawson = &AccountLawson1
		|			ИЛИ Док_ПроводкаDSS.AccountLawson = &AccountLawson2)
		|	И Док_ПроводкаDSS.AU.Сегмент.БазовыйЭлемент.Родитель = &SubSegmentLawson
		|	И Док_ПроводкаDSS.Проведен
		|	И (&ManagementGeomarket = ЗНАЧЕНИЕ(Справочник.ManagementGeography.ПустаяСсылка)
		|			ИЛИ Док_ПроводкаDSS.AU.ПодразделениеОрганизации.БазовыйЭлемент.GeoMarket.ManagementGeomarket = &ManagementGeomarket)
		|	&УсловиеСчета
		|	И Док_ПроводкаDSS.AccountingPeriod >= &НачалоПериода
		|	И Док_ПроводкаDSS.AccountingPeriod <= &КонецПериода";
	
	Запрос.УстановитьПараметр("AccountLawson1", ПланыСчетов.Lawson.TradeReceivables);
	Запрос.УстановитьПараметр("AccountLawson2", ПланыСчетов.Lawson.RevenueEstsUnbilled);
	Запрос.УстановитьПараметр("SubSegmentLawson", Справочники.HFM_Technology.НайтиПоКоду("ABL"));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ManagementGeomarket", ManagementGeomarket);
	
	Если ВидСчета = "Billed" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСчета", "И Док_ПроводкаDSS.AccountLawson.БазовыйЭлемент.Код = ""12001""");
	ИначеЕсли ВидСчета = "Unbilled" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСчета", "И НЕ Док_ПроводкаDSS.AccountLawson.БазовыйЭлемент.Код = ""12001""");
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСчета", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция НайтиТранзакцииOracle(Дата, ВидСчета, ManagementGeomarket) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТранзакцияOracle.Ссылка КАК Транзакция,
		|	ТранзакцияOracle.AU.ПодразделениеОрганизации.БазовыйЭлемент.GeoMarket.ManagementGeomarket КАК ManagementGeomarket,
		|	ТранзакцияOracle.Currency КАК Currency,
		|	ТранзакцияOracle.Client,
		|	ВЫБОР
		|		КОГДА ТранзакцияOracle.Account.Код ПОДОБНО ""110G.%""
		|			ТОГДА ТранзакцияOracle.Amount * 1.18
		|		ИНАЧЕ ТранзакцияOracle.Amount
		|	КОНЕЦ КАК Amount,
		|	ВЫБОР
		|		КОГДА ВнутренниеКурсыВалютСрезПоследних.Курс ЕСТЬ NULL
		|				ИЛИ ВнутренниеКурсыВалютСрезПоследних.Курс = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТранзакцияOracle.Account.Код ПОДОБНО ""110G.%""
		|					ТОГДА ТранзакцияOracle.Amount * 1.18
		|				ИНАЧЕ ТранзакцияOracle.Amount
		|			КОНЕЦ / ВнутренниеКурсыВалютСрезПоследних.Курс
		|	КОНЕЦ КАК BaseAmount,
		|	ВЫБОР
		|		КОГДА НЕ СоответсвиеКорректировкиРегистровПроводки.КорректировкаРегистров.Ссылка ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьКорректировка,
		|	КорректировкаТранзакции.Ссылка КАК КорректировкаТранзакции
		|ИЗ
		|	Документ.ТранзакцияOracle КАК ТранзакцияOracle
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВнутренниеКурсыВалют.СрезПоследних(&КонецПериода, ) КАК ВнутренниеКурсыВалютСрезПоследних
		|		ПО ТранзакцияOracle.Currency = ВнутренниеКурсыВалютСрезПоследних.Валюта
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответсвиеКорректировкиРегистровПроводки КАК СоответсвиеКорректировкиРегистровПроводки
		|		ПО ТранзакцияOracle.Ссылка = СоответсвиеКорректировкиРегистровПроводки.Транзакция
		|			И (НЕ СоответсвиеКорректировкиРегистровПроводки.Inactive)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаТранзакции КАК КорректировкаТранзакции
		|		ПО ТранзакцияOracle.Ссылка = КорректировкаТранзакции.ДокументОснование
		|			И (КорректировкаТранзакции.Проведен)
		|ГДЕ
		|	ТранзакцияOracle.Проведен
		|	И (ТранзакцияOracle.Account.Код ПОДОБНО ""110G%""
		|			ИЛИ ТранзакцияOracle.Account.Код ПОДОБНО ""1090%"")
		|	И ТранзакцияOracle.GL_Account ПОДОБНО ""%.999999""
		|	И ТранзакцияOracle.Дата >= &НачалоПериода
		|	И ТранзакцияOracle.Дата <= &КонецПериода
		|	И (&ManagementGeomarket = ЗНАЧЕНИЕ(Справочник.ManagementGeography.ПустаяСсылка)
		|			ИЛИ ТранзакцияOracle.AU.ПодразделениеОрганизации.БазовыйЭлемент.GeoMarket.ManagementGeomarket = &ManagementGeomarket)
		|	&УсловиеСчета
		|	И ТранзакцияOracle.Source = ЗНАЧЕНИЕ(Перечисление.ТипыСоответствий.OracleMI)";
	
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ManagementGeomarket", ManagementGeomarket);
	
	Если ВидСчета = "Billed" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСчета", "И ТранзакцияOracle.Account.БазовыйЭлемент.Код = ""12001""");
	ИначеЕсли ВидСчета = "Unbilled" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСчета", "И НЕ ТранзакцияOracle.Account.БазовыйЭлемент.Код = ""12001""");
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСчета", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#КонецЕсли