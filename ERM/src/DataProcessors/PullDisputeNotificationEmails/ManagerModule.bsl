#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьПисьма() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеУчетнойЗаписи = rgsУправлениеЭлектроннойПочтой.ПолучитьДанныеУчетнойЗаписи(rgsНастройкаКонфигурации.ЗначениеНастройки("УчетнаяЗаписьЭП_DisputeNotifications"));
	
	Профиль = РаботаСПочтовымиСообщениямиСлужебный.ИнтернетПочтовыйПрофиль(ДанныеУчетнойЗаписи.Ссылка, Истина);
	Протокол = ПротоколИнтернетПочты.POP3;
	Если ДанныеУчетнойЗаписи.ПротоколВходящейПочты = "IMAP" Тогда
		Протокол = ПротоколИнтернетПочты.IMAP;
	КонецЕсли;
	
	Почта = Новый ИнтернетПочта;
	Попытка
		Почта.Подключиться(Профиль, Протокол);
	Исключение
		
		ЕстьОшибки = Истина;
		
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Во время подключения к учетной записи %1 произошла ошибка
				|%2'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				ДанныеУчетнойЗаписи.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(rgsУправлениеЭлектроннойПочтой.СобытиеЖурналаРегистрации(),
			                         УровеньЖурналаРегистрации.Ошибка, , ,
			                         ТекстСообщенияОбОшибке);
		
		Возврат Неопределено;
		
	КонецПопытки;

	ПолученныеСообщения = Новый Массив;

	Если Протокол = ПротоколИнтернетПочты.POP3 Тогда
		ВызватьИсключение "Протокол POP3 не поддерживается"
	Иначе
		ФильтрПоЗаголовкам = Новый Соответствие;
		ПолученныеСообщения = rgsУправлениеЭлектроннойПочтой.ПолучитьПочтуПоПроколуIMAP(ДанныеУчетнойЗаписи, Почта, ФильтрПоЗаголовкам);
		//СинхронизироватьПризнакРассмотреноССервером(Почта, ДанныеУчетнойЗаписи, СтруктураМассивовСозданныхПисем.ВсеПолученныеПисьма);
	КонецЕсли;
	
	Почта.Отключиться();
	
	Если ТекстСообщенияОбОшибке <> Неопределено Тогда
		ВызватьИсключение ТекстСообщенияОбОшибке;	
	КонецЕсли;
	
	Возврат ПолученныеСообщения;
	
КонецФункции
	
Функция ОбработатьПисьма(Письма, ДанныеУчетнойЗаписи) Экспорт
	
	ОбработанныеПисьма = Новый Массив;
	
	Для Каждого Письмо Из Письма Цикл
		
		ОбработатьПисьмо(Письмо, ДанныеУчетнойЗаписи);
		ОбработанныеПисьма.Добавить(Письмо);
		
	КонецЦикла;
	
	Возврат ОбработанныеПисьма;
	
КонецФункции

Процедура ОбработатьПисьмо(Письмо, ДанныеУчетнойЗаписи) Экспорт
	
	НачатьТранзакцию();
	
	СозданноеПисьмо = rgsУправлениеЭлектроннойПочтой.ЗаписатьЭлектронноеПисьмо(ДанныеУчетнойЗаписи, Письмо, Ложь); 
	InvoiceProblems = ПолучитьКомментарииИнвойсовПоИдентификаторуПисьма(СозданноеПисьмо.ИдентификаторОснования);
	
	ДатаНовогоКомментария = ТекущаяДата();
	НЗ = РегистрыСведений.InvoiceComments.СоздатьНаборЗаписей();
	
	СозданныеКомментарии = Новый Массив;
	МассивИнвойсов = Новый Массив;
	
	Для Каждого InvoiceProblem Из InvoiceProblems Цикл
		НовыйКомментарийИнвойса = Документы.InvoiceProblem.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(НовыйКомментарийИнвойса, InvoiceProblem, , "Дата, Номер, RemedialWorkPlan, User");
		НовыйКомментарийИнвойса.User = rgsНастройкаКонфигурации.ЗначениеНастройки("AutoUser");
		НовыйКомментарийИнвойса.SLBAssignedTo.Загрузить(InvoiceProblem.SLBAssignedTo.Выгрузить());
		НовыйКомментарийИнвойса.Дата = ДатаНовогоКомментария;
		НовыйКомментарийИнвойса.CustomerInputDetails = Формат(ДатаНовогоКомментария, "ДФ=M.d.yyyy") + " - " + СозданноеПисьмо.Текст;
		НовыйКомментарийИнвойса.Записать();
		ЗаписьНабора = НЗ.Добавить();
		ЗаписьНабора.Период = ДатаНовогоКомментария;
		ЗаписьНабора.Invoice = НовыйКомментарийИнвойса.Invoice;
		ЗаписьНабора.Problem = НовыйКомментарийИнвойса.Ссылка;
		ОтменитьЗадачуПоНотификации(InvoiceProblem);
		МассивИнвойсов.Добавить(НовыйКомментарийИнвойса.Invoice);
		СозданныеКомментарии.Добавить(НовыйКомментарийИнвойса.Ссылка);
	КонецЦикла;
	НЗ.Записать(Ложь);
	
	РегистрыСведений.EmailsObjects.ЗарегистрироватьПредметыПисьма(СозданноеПисьмо, СозданныеКомментарии, Перечисления.ВариантОтправленияСообщения.Escalation);
	РегистрыСведений.EmailsObjects.ЗарегистрироватьПредметыПисьма(СозданноеПисьмо, МассивИнвойсов, Перечисления.ВариантОтправленияСообщения.Escalation);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Функция ПолучитьКомментарииИнвойсовПоИдентификаторуПисьма(ИдентификаторПисьма)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	EmailsObjects.Предмет
		|ИЗ
		|	РегистрСведений.EmailsObjects КАК EmailsObjects
		|ГДЕ
		|	EmailsObjects.Email.ИдентификаторСообщения = &ИдентификаторСообщения
		|	И EmailsObjects.Предмет ССЫЛКА Документ.InvoiceProblem";
	
	Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторПисьма);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Предмет");
	
КонецФункции

Процедура ОтменитьЗадачуПоНотификации(InvoiceProblem)
	
	НЗ = РегистрыСведений.ОчередьУведомлений.СоздатьНаборЗаписей();
	НЗ.Отбор.Проблема.Установить(InvoiceProblem);
	НЗ.Отбор.ВидУведомления.Установить(Справочники.ВидыУведомлений.Dispute);
	НЗ.Записать(Истина);
	
КонецПроцедуры

#КонецЕсли