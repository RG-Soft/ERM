
&НаКлиенте
Процедура Печать(Команда)
	
	ИдентификаторПечатнойФормы = "ПечатнаяФорма";
	НазваниеПечатнойФормы = НСтр("ru='Collection target unbilled details'");
	
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
	ПечатнаяФорма = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
	ПечатнаяФорма.СинонимМакета         = НазваниеПечатнойФормы;
	ПечатнаяФорма.ТабличныйДокумент     = ПреобразоватьРезультатОтчета();
	ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
	
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
	
КонецПроцедуры

&НаКлиенте
Функция ПреобразоватьРезультатОтчета()
	
	ПреобразоватьРезультатОтчетаНаСервере();
	
	Возврат РезультатКопия;
	
КонецФункции

&НаСервере
Процедура ПреобразоватьРезультатОтчетаНаСервере()
	
	РезультатКопия = Новый ТабличныйДокумент;
	РезультатКопия.Вывести(Результат);
	
	ДанныеРасшифровкиОбъект = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	Для НомерСтолбца = 1 По РезультатКопия.ШиринаСтраницы Цикл
		Для НомерСтроки = 1 По РезультатКопия.ВысотаТаблицы Цикл
			ТекущаяОбласть =  РезультатКопия.Область(НомерСтроки, НомерСтолбца);
			Если ТекущаяОбласть.Расшифровка <> Неопределено Тогда
				ЗначениеРасшифровки = ДанныеРасшифровкиОбъект.Элементы[ТекущаяОбласть.Расшифровка].ПолучитьПоля()[0].Значение;
				Если ТипЗнч(ЗначениеРасшифровки) = Тип("Дата") Тогда
					ДатаЧислом = (ЗначениеРасшифровки - Дата('18991230000000'))/(24*60*60);
					Если ДатаЧислом > 0 Тогда
						ДатаЧислом = Формат(ДатаЧислом,"ЧДЦ=5; ЧГ=0");
						ТекущаяОбласть.Текст = ДатаЧислом;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры


