
Процедура ВыгрузитьЭксельВТаблицуДанных(ПутьКФайлу, ТаблицаДанных, ДанныеДляЗаполнения, АдресХранилища, СтруктураПараметров) Экспорт
	
	Connection = Новый COMОбъект("ADODB.Connection");
	СтрокаПодключения = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" + СокрЛП(ПутьКФайлу) + ";Extended Properties=""Excel 12.0 Xml;HDR=" + ?(СтруктураПараметров.ИменаКолонокВПервойСтроке, "Yes", "No") + """";
	
	Попытка
		Connection.Open(СтрокаПодключения);
	Исключение
		Попытка
			СтрокаПодключения = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" + СокрЛП(ПутьКФайлу) + ";Extended Properties=""Excel 8.0;HDR=" + ?(СтруктураПараметров.ИменаКолонокВПервойСтроке, "Yes", "No") + """";
			Connection.Open(СтрокаПодключения);
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			ДанныеДляЗаполнения.Вставить("ОшибкаЗаполнения", ТекстОшибки);
			ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
			Возврат;
		КонецПопытки;
	КонецПопытки;
	
	rs = Новый COMObject("ADODB.RecordSet");
	rs.ActiveConnection = Connection;
	sqlString = "select * from [" + СтруктураПараметров.ЛистФайла + "]";
	rs.Open(sqlString);
	
	СтруктураКолонок = СтруктураПараметров.СтруктураКолонок;
	
	// проверим формат файла
	Если Не СтруктураПараметров.ИменаКолонокВПервойСтроке Тогда
		// тогда поищем строку с именами колонок, она должна быть до первой строки данных
		// будем ориентироваться по первой же колонке, если там не найдем, то проблема уже есть
		СтруктураКолонкиДляПроверки = СтруктураКолонок[0];
		ТекНомерСтроки = 1;
		Пока Врег(rs.Fields(СтруктураКолонкиДляПроверки.НомерКолонки - 1).Name) <> Врег(СтруктураКолонкиДляПроверки.ИмяКолонки)
			И ТекНомерСтроки < СтруктураПараметров.ПерваяСтрокаДанных Цикл
			ТекНомерСтроки = ТекНомерСтроки + 1;
			rs.MoveNext();
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтруктураКолонкиДляПроверки Из СтруктураКолонок Цикл
		Если Врег(rs.Fields(СтруктураКолонкиДляПроверки.НомерКолонки - 1).Name) <> Врег(СтруктураКолонкиДляПроверки.ИмяКолонки) Тогда
			ДанныеДляЗаполнения.Вставить("ОшибкаЗаполнения", "Invalid input file format. Please see file settings.");
			ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	rs.MoveFirst();
	
	ВеличинаСдвига = СтруктураПараметров.ПерваяСтрокаДанных - 1 - ?(СтруктураПараметров.ИменаКолонокВПервойСтроке, 1, 0);
	Если ВеличинаСдвига <> 0 Тогда
		rs.Move(ВеличинаСдвига);
	КонецЕсли;
	
	ТекНомерСтроки = СтруктураПараметров.ПерваяСтрокаДанных;
	ПоследняяСтрокаДанных = СтруктураПараметров.ПоследняяСтрокаДанных;
	
	Пока Не rs.EOF И (ТекНомерСтроки <= ПоследняяСтрокаДанных ИЛИ ПоследняяСтрокаДанных = 0) Цикл
		
		СтрокаТЗ = ТаблицаДанных.Добавить();
		СтрокаТЗ.СтрокаФайла = ТекНомерСтроки;
		
		Для каждого СтруктураКолонки Из СтруктураКолонок Цикл
			
			Значение = rs.Fields(СтруктураКолонки.НомерКолонки - 1).Value;
			Если ТипЗнч(СтрокаТЗ[СтруктураКолонки.ИмяПоля]) = Тип("Строка") И ТипЗнч(Значение) = Тип("Число") Тогда
				Значение = Формат(Значение, "ЧДЦ=0; ЧГ=0");
			ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
				Значение = СокрЛП(Значение);
			КонецЕсли;
			СтрокаТЗ[СтруктураКолонки.ИмяПоля] = Значение;
			
		КонецЦикла; 
		
		rs.MoveNext();
		ТекНомерСтроки = ТекНомерСтроки + 1;
		
	КонецЦикла;
	
	rs.Close();
	Connection.Close();
	УдалитьФайлы(ПутьКФайлу);

КонецПроцедуры

Процедура ВыгрузитьЭксельВТаблицуДанныхПоИменамКолонок(ПутьКФайлу, ТаблицаДанных, ДанныеДляЗаполнения, АдресХранилища, СтруктураПараметров) Экспорт
	
	Connection = Новый COMОбъект("ADODB.Connection");
	СтрокаПодключения = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" + СокрЛП(ПутьКФайлу) + ";Extended Properties=""Excel 12.0 Xml;HDR=" + ?(СтруктураПараметров.ИменаКолонокВПервойСтроке, "Yes", "No") + """";
	
	Попытка
		Connection.Open(СтрокаПодключения);
	Исключение
		Попытка
			СтрокаПодключения = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" + СокрЛП(ПутьКФайлу) + ";Extended Properties=""Excel 8.0;HDR=" + ?(СтруктураПараметров.ИменаКолонокВПервойСтроке, "Yes", "No") + """";
			Connection.Open(СтрокаПодключения);
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			ДанныеДляЗаполнения.Вставить("ОшибкаЗаполнения", ТекстОшибки);
			ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
			Возврат;
		КонецПопытки;
	КонецПопытки;
	
	rs = Новый COMObject("ADODB.RecordSet");
	rs.ActiveConnection = Connection;
	sqlString = "select * from [" + СтруктураПараметров.ЛистФайла + "]";
	rs.Open(sqlString);
	
	СтруктураКолонок = СтруктураПараметров.СтруктураКолонок;
	КолонкиИсключения = Новый Массив;
	
	// проверим формат файла
	Если Не СтруктураПараметров.ИменаКолонокВПервойСтроке Тогда
		// тогда поищем строку с именами колонок, она должна быть до первой строки данных
		// будем ориентироваться по первой же колонке, если там не найдем, то проблема уже есть
		СтруктураКолонкиДляПроверки = СтруктураКолонок[0];
		ТекНомерСтроки = 1;
		Пока rs.Fields(СтруктураКолонкиДляПроверки.ИмяКолонки) = Неопределено
			И ТекНомерСтроки < СтруктураПараметров.ПерваяСтрокаДанных Цикл
			ТекНомерСтроки = ТекНомерСтроки + 1;
			rs.MoveNext();
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтруктураКолонкиДляПроверки Из СтруктураКолонок Цикл
		Попытка
			ТекЗначение = rs.Fields(СтруктураКолонкиДляПроверки.ИмяКолонки);
		Исключение
			Если СтруктураКолонкиДляПроверки.Обязательная Тогда
				ДанныеДляЗаполнения.Вставить("ОшибкаЗаполнения", "Invalid input file format. Please see file settings.");
				ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
				Возврат;
			Иначе
				КолонкиИсключения.Добавить(СтруктураКолонкиДляПроверки.ИмяКолонки);
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	rs.MoveFirst();
	
	ВеличинаСдвига = СтруктураПараметров.ПерваяСтрокаДанных - 1 - ?(СтруктураПараметров.ИменаКолонокВПервойСтроке, 1, 0);
	Если ВеличинаСдвига <> 0 Тогда
		rs.Move(ВеличинаСдвига);
	КонецЕсли;
	
	ТекНомерСтроки = СтруктураПараметров.ПерваяСтрокаДанных;
	ПоследняяСтрокаДанных = СтруктураПараметров.ПоследняяСтрокаДанных;
	
	ТипСтрока = Тип("Строка");
	ТипЧисло = Тип("Число");
	МетаданныеРесурсов = Метаданные.РегистрыСведений[СтруктураПараметров.ИмяРегистра].Ресурсы;
	
	Пока Не rs.EOF И (ТекНомерСтроки <= ПоследняяСтрокаДанных ИЛИ ПоследняяСтрокаДанных = 0) Цикл
		
		СтрокаТЗ = ТаблицаДанных.Добавить();
		СтрокаТЗ.СтрокаФайла = ТекНомерСтроки;
		
		Для каждого СтруктураКолонки Из СтруктураКолонок Цикл
			
			Если КолонкиИсключения.Найти(СтруктураКолонки.ИмяКолонки) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Значение = rs.Fields(СтруктураКолонки.ИмяКолонки).Value;
			Если МетаданныеРесурсов[СтруктураКолонки.ИмяПоля].Тип.СодержитТип(ТипСтрока)
				И НЕ МетаданныеРесурсов[СтруктураКолонки.ИмяПоля].Тип.СодержитТип(ТипЧисло) 
				И ТипЗнч(Значение) = Тип("Число") Тогда
				Значение = Формат(Значение, "ЧДЦ=0; ЧГ=0");
			ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
				Значение = СокрЛП(Значение);
			КонецЕсли;
			СтрокаТЗ[СтруктураКолонки.ИмяПоля] = Значение;
			
		КонецЦикла; 
		
		rs.MoveNext();
		ТекНомерСтроки = ТекНомерСтроки + 1;
		
	КонецЦикла;
	
	rs.Close();
	Connection.Close();
	УдалитьФайлы(ПутьКФайлу);

КонецПроцедуры

