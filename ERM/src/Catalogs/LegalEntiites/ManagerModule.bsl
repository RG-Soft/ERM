#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область PowerBI

// Описание
// Выполняет выгрузку данных в Power BI
// Параметры:
// 	Ссылка - Ссылка на данные, которые нужно выгрузить
// 	Операция - вид операции с данными
// Возвращаемое значение:
// 	Структура - включает поля Результат, Сообщение.
Функция ВыгрузитьДанныеДляPowerBI(Ссылка, Операция, ПараметрыЛогирования) Экспорт
	
	СтруктураРезультата = Новый Структура("Результат, Сообщение", Ложь, "");
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Код, ПометкаУдаления, Наименование, Владелец, Source");
	
	Если Операция = Перечисления.ОперацииPowerBI.Create Тогда
		ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.СозданиеОбъекта;
		РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
		ТекОбъект = ВнешниеИсточникиДанных.ERM_BI.Таблицы.dbo_LegalEntiites.СоздатьОбъект();
		ТекОбъект.ID = Строка(Ссылка.УникальныйИдентификатор());
	Иначе
		ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.ПолучениеОбъекта;
		РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
		СсылкаВоВнешнемИсточнике = ВнешниеИсточникиДанных.ERM_BI.Таблицы.dbo_LegalEntiites.ПолучитьСсылку(Строка(Ссылка.УникальныйИдентификатор()));
		ТекОбъект = СсылкаВоВнешнемИсточнике.ПолучитьОбъект();
	КонецЕсли;
	ТекОбъект.DeletionMark = СтруктураРеквизитов.ПометкаУдаления;
	ТекОбъект.Description = СтруктураРеквизитов.Наименование;
	ТекОбъект.Code = СтруктураРеквизитов.Код;
	БазовыйЭлемент = Неопределено;
	Если ЗначениеЗаполнено(СтруктураРеквизитов.Владелец) Тогда
		БазовыйЭлемент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураРеквизитов.Владелец, "БазовыйЭлемент");
	КонецЕсли;
	Если ЗначениеЗаполнено(БазовыйЭлемент) Тогда
		ТекОбъект.OwnerID = Строка(БазовыйЭлемент.УникальныйИдентификатор());
	Иначе
		ТекОбъект.OwnerID = NULL;
	КонецЕсли;
	ТекОбъект.Source = Строка(СтруктураРеквизитов.Source);
	ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.НачалоЗаписи;
	РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
	Попытка
		ТекОбъект.Записать();
	Исключение
		СтруктураРезультата.Сообщение = ОписаниеОшибки();
		Возврат СтруктураРезультата;
	КонецПопытки;
	ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.ОкончаниеЗаписи;
	РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
	
	СтруктураРезультата.Результат = Истина;
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли