
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НайтиПоEmail(Email) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	LDAPUsers.Ссылка
		|ИЗ
		|	Справочник.LDAPUsers КАК LDAPUsers
		|ГДЕ
		|	LDAPUsers.Mail = &Mail
		|	И НЕ LDAPUsers.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Mail", Email);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.LDAPUsers.ПустаяСсылка();
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
	
КонецФункции

Функция ПолучитьDirectManagerПоДаннымLDAP(Ссылка) Экспорт

	ManagerDN = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ManagerDN");
	МассивПодстрок = СтрРазделить(ManagerDN, ",");
	DirectManagerCN = "";
	Для каждого ТекПодстрока Из МассивПодстрок Цикл
		Если СтрНачинаетсяС(ТекПодстрока, "CN=") Тогда
			DirectManagerCN = СокрЛП(Прав(ТекПодстрока, СтрДлина(ТекПодстрока) - 3));
			Прервать;			
		КонецЕсли;		
	КонецЦикла;
	
	Если ПустаяСтрока(DirectManagerCN) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	LDAPUsers.Ссылка
		|ИЗ
		|	Справочник.LDAPUsers КАК LDAPUsers
		|ГДЕ
		|	LDAPUsers.CN = &CN
		|	И
		|	НЕ LDAPUsers.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("CN", DirectManagerCN);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();

	Возврат ВыборкаДетальныеЗаписи.Ссылка;

КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Наименование");
	Поля.Добавить("Mail");
	
КонецПроцедуры 

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = Данные.Наименование + " (" + Данные.Mail + ")";
	
КонецПроцедуры
#КонецОбласти
