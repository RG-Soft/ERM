#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ВыполнитьЗадание() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыЛогирования = РегистрыСведений.ЛогВыгрузкиPowerBI.НовыеПараметрыЛогирования();
	ПараметрыЛогирования.Источник = Источник;
	ПараметрыЛогирования.Операция = Операция;
	ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.НачалоВыполненияЗадания;
	ПараметрыЛогирования.Комментарий = Справочники.ЗаданияPowerBI.ОписаниеЗадания(ЭтотОбъект);
	РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
	ПараметрыЛогирования.Комментарий = "";
	
	СтруктураРезультата = Новый Структура("Результат, Сообщение", Ложь, "");
	
	Если НЕ Родитель.Пустая() Тогда
		ТекстСообщения = СтрШаблон("Не возможно выполнить задание %1, т.к. существует родительское задание %2.", ЭтотОбъект, Родитель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ПараметрыЛогирования.ЭтоОшибка = Истина;
		ПараметрыЛогирования.Комментарий = СтруктураРезультата.Сообщение;
		РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
		СтруктураРезультата.Результат = Ложь;
		СтруктураРезультата.Сообщение = ТекстСообщения;
		Возврат СтруктураРезультата;
	КонецЕсли;
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Источник));
	
	Попытка
		Если ОбщегоНазначения.ЭтоСправочник(ОбъектМетаданных) Тогда
			РезультатВыгрузки = Справочники[ОбъектМетаданных.Имя].ВыгрузитьДанныеДляPowerBI(Источник, Операция, ПараметрыЛогирования);
		ИначеЕсли ОбщегоНазначения.ЭтоПланСчетов(ОбъектМетаданных) Тогда
			РезультатВыгрузки = ПланыСчетов[ОбъектМетаданных.Имя].ВыгрузитьДанныеДляPowerBI(Источник, Операция, ПараметрыЛогирования);
		ИначеЕсли ОбщегоНазначения.ЭтоДокумент(ОбъектМетаданных) Тогда
			РезультатВыгрузки = Документы[ОбъектМетаданных.Имя].ВыгрузитьДанныеДляPowerBI(Источник, Операция, ПараметрыЛогирования);
		Иначе
			ВызватьИсключение СтрШаблон("Unexpected metadata type: %1", Источник);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтруктураРезультата, РезультатВыгрузки);
	Исключение
		СтруктураРезультата.Сообщение = ОписаниеОшибки();
		ПараметрыЛогирования.ЭтоОшибка = Истина;
		ПараметрыЛогирования.Комментарий = СтруктураРезультата.Сообщение;
	КонецПопытки;
	
	ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.ОкончаниеВыполненияЗадания;
	РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
	
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецЕсли