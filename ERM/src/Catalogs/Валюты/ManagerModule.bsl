#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("СпособУстановкиКурса");
	Результат.Добавить("Наценка");
	Результат.Добавить("ОсновнаяВалюта");
	Результат.Добавить("ФормулаРасчетаКурса");
	Возврат Результат;
	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

#Область PowerBI

// Описание
// Выполняет выгрузку данных в Power BI
// Параметры:
// 	Ссылка - Ссылка на данные, которые нужно выгрузить
// 	Операция - вид операции с данными
// Возвращаемое значение:
// 	Структура - включает поля Результат, Сообщение.
Функция ВыгрузитьДанныеДляPowerBI(Ссылка, Операция, ПараметрыЛогирования) Экспорт
	
	СтруктураРезультата = Новый Структура("Результат, Сообщение", Ложь, "");
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Код, ПометкаУдаления, Наименование, НаименованиеПолное");
	
	Если Операция = Перечисления.ОперацииPowerBI.Create Тогда
		ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.СозданиеОбъекта;
		РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
		ТекОбъект = ВнешниеИсточникиДанных.ERM_BI.Таблицы.dbo_Currencies.СоздатьОбъект();
		ТекОбъект.ID = Строка(Ссылка.УникальныйИдентификатор());
	Иначе
		ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.ПолучениеОбъекта;
		РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
		СсылкаВоВнешнемИсточнике = ВнешниеИсточникиДанных.ERM_BI.Таблицы.dbo_Currencies.ПолучитьСсылку(Строка(Ссылка.УникальныйИдентификатор()));
		ТекОбъект = СсылкаВоВнешнемИсточнике.ПолучитьОбъект();
	КонецЕсли;
	ТекОбъект.Code = СтруктураРеквизитов.Код;
	ТекОбъект.DeletionMark = СтруктураРеквизитов.ПометкаУдаления;
	ТекОбъект.Description = СтруктураРеквизитов.Наименование;
	ТекОбъект.FullName = СтруктураРеквизитов.НаименованиеПолное;
	ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.НачалоЗаписи;
	РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
	Попытка
		ТекОбъект.Записать();
	Исключение
		СтруктураРезультата.Сообщение = ОписаниеОшибки();
		Возврат СтруктураРезультата;
	КонецПопытки;
	ПараметрыЛогирования.Событие = Перечисления.ТипыСобытийИнтеграцииPowerBI.ОкончаниеЗаписи;
	РегистрыСведений.ЛогВыгрузкиPowerBI.ЗалогироватьСобытие(ПараметрыЛогирования);
	СтруктураРезультата.Результат = Истина;
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли