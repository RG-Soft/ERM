#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения            = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаПараметрыПроведения(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	ТаблицаСвязанныеДокументы = Результат[НомераТаблиц["СвязанныеДокументы"]].Выгрузить();
	
	ПараметрыПроведения.Вставить("СвязанныеДокументы", Новый Структура("SalesOrder, Invoice, CashBatch, BatchAllocation, РучнаяКорректировка, Memo"));
	
	Для каждого СтрокаСвязанногоДокумента Из ТаблицаСвязанныеДокументы Цикл
		Если СтрокаСвязанногоДокумента.ТипСвязанногоОбъекта = Перечисления.ТипыОбъектовСвязанныхСПроводкойDSS.SalesOrder Тогда
			ПараметрыПроведения.СвязанныеДокументы.SalesOrder = СтрокаСвязанногоДокумента.Ссылка;
		ИначеЕсли СтрокаСвязанногоДокумента.ТипСвязанногоОбъекта = Перечисления.ТипыОбъектовСвязанныхСПроводкойDSS.Invoice Тогда
			ПараметрыПроведения.СвязанныеДокументы.Invoice = СтрокаСвязанногоДокумента.Ссылка;
		ИначеЕсли СтрокаСвязанногоДокумента.ТипСвязанногоОбъекта = Перечисления.ТипыОбъектовСвязанныхСПроводкойDSS.CashBatch Тогда
			ПараметрыПроведения.СвязанныеДокументы.CashBatch = СтрокаСвязанногоДокумента.Ссылка;
		ИначеЕсли СтрокаСвязанногоДокумента.ТипСвязанногоОбъекта = Перечисления.ТипыОбъектовСвязанныхСПроводкойDSS.BatchAllocation Тогда
			ПараметрыПроведения.СвязанныеДокументы.BatchAllocation = СтрокаСвязанногоДокумента.Ссылка;
		ИначеЕсли СтрокаСвязанногоДокумента.ТипСвязанногоОбъекта = Перечисления.ТипыОбъектовСвязанныхСПроводкойDSS.РучнаяКорректировка Тогда
			ПараметрыПроведения.СвязанныеДокументы.РучнаяКорректировка = СтрокаСвязанногоДокумента.Ссылка;
		ИначеЕсли СтрокаСвязанногоДокумента.ТипСвязанногоОбъекта = Перечисления.ТипыОбъектовСвязанныхСПроводкойDSS.Memo Тогда
			ПараметрыПроведения.СвязанныеДокументы.Memo = СтрокаСвязанногоДокумента.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаПараметрыПроведения(НомераТаблиц)
	
	НомераТаблиц.Вставить("Реквизиты",                       НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СвязанныеДокументы",              НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТранзакцияHOB.Ссылка,
	|	ТранзакцияHOB.ВерсияДанных,
	|	ТранзакцияHOB.ПометкаУдаления,
	|	ТранзакцияHOB.Номер,
	|	ТранзакцияHOB.Дата,
	|	ТранзакцияHOB.Проведен,
	|	ТранзакцияHOB.TransactionType,
	|	ТранзакцияHOB.Company,
	|	ТранзакцияHOB.Account,
	|	ТранзакцияHOB.Location,
	|	ТранзакцияHOB.SubSubSegment,
	|	ТранзакцияHOB.Currency,
	|	ТранзакцияHOB.ExchangeRate,
	|	ТранзакцияHOB.Client,
	|	ТранзакцияHOB.Contract,
	|	ТранзакцияHOB.Amount,
	|	ТранзакцияHOB.BaseAmount,
	|	ТранзакцияHOB.Ответственный,
	|	ТранзакцияHOB.DocumentID,
	|	ТранзакцияHOB.DocumentPresentation,
	|	ТранзакцияHOB.TrNumber,
	|	ТранзакцияHOB.TrID,
	|	ТранзакцияHOB.SalesOrderID,
	|	ТранзакцияHOB.InvoiceID,
	|	ТранзакцияHOB.CREW,
	|	ТранзакцияHOB.Reverse,
	|	ТранзакцияHOB.AU,
	|	ТранзакцияHOB.HOBDocumentType,
	|	ТранзакцияHOB.HOBInvoiceType,
	|	ТранзакцияHOB.LegalEntity,
	|	ТранзакцияHOB.CorAccount,
	|	ТранзакцияHOB.Account.БазовыйЭлемент КАК HFMAccount,
	|	ТранзакцияHOB.HOBReverseType,
	|	ТранзакцияHOB.MNGC,
	|	ТранзакцияHOB.EndClient
	|ИЗ
	|	Документ.ТранзакцияHOB КАК ТранзакцияHOB
	|ГДЕ
	|	ТранзакцияHOB.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	DSSСвязанныеДокументы.ТипСвязанногоОбъекта,
	|	DSSСвязанныеДокументы.СвязанныйОбъект КАК Ссылка
	|ИЗ
	|	РегистрСведений.DSSСвязанныеДокументы КАК DSSСвязанныеДокументы
	|ГДЕ
	|	DSSСвязанныеДокументы.ПроводкаDSS = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьСуммыДляПроводки(Реквизиты, СчетВыручкиHFM, ВалютаUSD, Отказ) Экспорт
	
	СуммыДляПроводки = Новый Структура("Amount,BaseAmount");	
	СуммыДляПроводки.Amount = Реквизиты.Amount;

	Если Реквизиты.HFMAccount.ПринадлежитЭлементу(СчетВыручкиHFM) Тогда
		
		Если Реквизиты.Currency = ВалютаUSD Тогда
			СуммыДляПроводки.BaseAmount = Реквизиты.Amount;
		Иначе
			СтруктураКурсаВалюты = РаботаСКурсамиВалют.ПолучитьВнутреннийКурсВалюты(Реквизиты.Currency, Реквизиты.Дата);
			Если Не ЗначениеЗаполнено(СтруктураКурсаВалюты.Курс) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("No exchange rate " + Реквизиты.Currency, , , , Отказ);
			КонецЕсли;
			СуммыДляПроводки.BaseAmount = Реквизиты.Amount / СтруктураКурсаВалюты.Курс * ?(СтруктураКурсаВалюты.Кратность = 0, 1, СтруктураКурсаВалюты.Кратность);
		КонецЕсли;
		
	Иначе
		
		СуммыДляПроводки.BaseAmount = Реквизиты.BaseAmount;
		
	КонецЕсли;
	
	Возврат СуммыДляПроводки;
	
КонецФункции

#КонецЕсли