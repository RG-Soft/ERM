#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		Reverse = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		Reverse = Истина;
	КонецЕсли;

КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ Reverse И НЕ РольДоступна("ПолныеПрава") И НЕ РольДоступна("rgsСозданиеКорректировокРегистровСРеверсом") И НЕ ДополнительныеСвойства.Свойство("РазрешитьСозданиеДокументаБезРеверса") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("You are not allowed to create a register adjustment without Reverse",,,, Отказ);
	КонецЕсли;
	
	Если Reverse Тогда
		
		МассивРегистров = Новый Массив;
		МассивРегистров.Добавить("BilledAR");
		МассивРегистров.Добавить("UnbilledAR");
		МассивРегистров.Добавить("UnallocatedCash");
		МассивРегистров.Добавить("UnallocatedMemo");
		МассивРегистров.Добавить("ManualTransactions");
		
		Для Каждого Регистр Из МассивРегистров Цикл
		
			Если Движения[Регистр].Количество() > 0 Тогда
				
				МассивДляУдаления = Новый Массив;
				
				Для каждого Движение Из Движения[Регистр] Цикл
					Если Движение.Reverse Тогда 
						МассивДляУдаления.Добавить(Движение);
					КонецЕсли;
				КонецЦикла;
				
				Для каждого ЭлементМассива Из МассивДляУдаления Цикл
					Движения[Регистр].Удалить(ЭлементМассива);
				КонецЦикла;
				
				КоличествоЗаписей = Движения[Регистр].Количество();
				
				Для НомерСтроки = 0 По КоличествоЗаписей - 1 Цикл
					НовоеДвижение = Движения[Регистр].Добавить();
					ЗаполнитьЗначенияСвойств(НовоеДвижение, Движения[Регистр][НомерСтроки]);
					НовоеДвижение.Reverse = Истина;
					НовоеДвижение.Период = КонецМесяца(НовоеДвижение.Период) + 1;
					НовоеДвижение.Amount = -НовоеДвижение.Amount;
					НовоеДвижение.BaseAmount = -НовоеДвижение.BaseAmount;
				КонецЦикла;
				
			КонецЕсли;
	
		КонецЦикла;
				
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ЭтоНовый() И Ссылка.ПометкаУдаления <> ПометкаУдаления Тогда
		УстановитьАктивностьДвижений(НЕ ПометкаУдаления);
	ИначеЕсли ПометкаУдаления Тогда
		УстановитьАктивностьДвижений(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьАктивностьДвижений(ФлагАктивности)
	
	Для Каждого Движение Из Движения Цикл
		
		Движение.Прочитать();
		Движение.УстановитьАктивность(ФлагАктивности);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли