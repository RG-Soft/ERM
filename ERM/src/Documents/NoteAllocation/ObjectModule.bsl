#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ВидОперацииNoteAllocation = Перечисления.ВидыОперацийNoteAllocation.ПустаяСсылка() Тогда
		
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Select the operation type before recording.");
		
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Ответственный = Пользователи.ТекущийПользователь();
		CreatedDate = ТекущаяДата();
		
	Иначе
		
		ModifiedBy = Пользователи.ТекущийПользователь();
		ModificationDate = ТекущаяДата();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ТЗ_Документы =  CreditNotes.Выгрузить(,"CreditNote");
	ТЗ_НоваяСтрока = ТЗ_Документы.Добавить();
	ТЗ_НоваяСтрока.CreditNote = Invoice;
	
	ТЗ_Memo = Новый ТаблицаЗначений;
	ТЗ_Memo.Колонки.Добавить("Memo");
	
	Для каждого СтрокаТЗ Из ТЗ_Документы Цикл
		
		Если ТипЗнч(СтрокаТЗ.CreditNote) = Тип("ДокументСсылка.Memo") Тогда
			ТЗ_НоваяСтрока = ТЗ_Memo.Добавить();
			ТЗ_НоваяСтрока.Memo = СтрокаТЗ.CreditNote;
		КонецЕсли;
		
	КонецЦикла;
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировкиBilledAR = Блокировка.Добавить("РегистрНакопления.BilledAR");
	ЭлементБлокировкиBilledAR.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировкиBilledAR.ИсточникДанных = ТЗ_Документы;
	ЭлементБлокировкиBilledAR.ИспользоватьИзИсточникаДанных("Invoice","CreditNote");
	ЭлементБлокировкиUnallocatedMemo = Блокировка.Добавить("РегистрНакопления.UnallocatedMemo");
	ЭлементБлокировкиUnallocatedMemo.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировкиUnallocatedMemo.ИсточникДанных = ТЗ_Memo;
	ЭлементБлокировкиUnallocatedMemo.ИспользоватьИзИсточникаДанных("Memo","Memo");
	Блокировка.Заблокировать();
	
	ПроведениеВозможно = ПроверитьНаличиеБалансовПоДокументам();
	
	Если Не ПроведениеВозможно Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Проведение запрещено! По выбранным документам нет балансов на " + Дата + " .",,,,Отказ);
		Возврат;
	КонецЕсли;
	
	СтруктураКурсаInvoice = РаботаСКурсамиВалют.ПолучитьВнутреннийКурсВалюты(Currency, Дата);
	
	Если ВидОперацииNoteAllocation = Перечисления.ВидыОперацийNoteAllocation.CreditNote Тогда
		
		ДанныеКлиента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Invoice, "Client, ClientID");
		
		ДвиженияBilledAR = Движения.BilledAR;
		
		Для каждого СтрокаТЧ Из CreditNotes Цикл
			
			ДвижениеCreditNote = ДвиженияBilledAR.Добавить();
			ДвижениеInvoice = ДвиженияBilledAR.Добавить();
			
			СтруктураКурсаCreditNote = РаботаСКурсамиВалют.ПолучитьВнутреннийКурсВалюты(СтрокаТЧ.Currency, Дата);
			
			ДвижениеCreditNote.Период = Дата;
			ДвижениеCreditNote.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеCreditNote.Client = ДанныеКлиента.Client;
			ДвижениеCreditNote.ClientID = ДанныеКлиента.ClientID;
			ДвижениеCreditNote.Company = СтрокаТЧ.Company;
			ДвижениеCreditNote.Source = Source;
			ДвижениеCreditNote.Location = СтрокаТЧ.Location;
			ДвижениеCreditNote.SubSubSegment = СтрокаТЧ.SubSubSegment;
			ДвижениеCreditNote.Invoice = СтрокаТЧ.CreditNote;
			ДвижениеCreditNote.Account = СтрокаТЧ.Account;
			ДвижениеCreditNote.Currency = СтрокаТЧ.Currency;
			ДвижениеCreditNote.AU = СтрокаТЧ.AU;
			ДвижениеCreditNote.Amount = -СтрокаТЧ.Amount;
			ДвижениеCreditNote.BaseAmount = -СтрокаТЧ.Amount / ?(ЗначениеЗаполнено(СтруктураКурсаCreditNote.Курс), СтруктураКурсаCreditNote.Курс, 1);
			
			ДвижениеInvoice.Период = Дата;
			ДвижениеInvoice.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеInvoice.Client = ДанныеКлиента.Client;
			ДвижениеInvoice.ClientID = ДанныеКлиента.ClientID;
			ДвижениеInvoice.Company = Company;
			ДвижениеInvoice.Source = Source;
			ДвижениеInvoice.Location = Location;
			ДвижениеInvoice.SubSubSegment = SubSubSegment;
			ДвижениеInvoice.Invoice = Invoice;
			ДвижениеInvoice.Account = Account;
			ДвижениеInvoice.Currency = Currency;
			ДвижениеInvoice.AU = AU;
			ДвижениеInvoice.Amount = -ДвижениеCreditNote.Amount;
			ДвижениеInvoice.BaseAmount = -ДвижениеCreditNote.BaseAmount;
			
		КонецЦикла;
		
		ДвиженияBilledAR.Записывать = Истина;
		
	ИначеЕсли ВидОперацииNoteAllocation = Перечисления.ВидыОперацийNoteAllocation.CreditMemo Тогда
	
		Если ТипЗнч(Invoice) <> Тип("ДокументСсылка.Memo") Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В поле Memo необходимо указать Memo либо выбрать другой тип Note allocation.",,,,Отказ);
			Возврат;
		КонецЕсли;
		
		ДвиженияUnallocatedMemo = Движения.UnallocatedMemo;
		ДвиженияBilledAR = Движения.BilledAR;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	UnallocatedMemoОстатки.AmountОстаток,
			|	UnallocatedMemoОстатки.BaseAmountОстаток
			|ИЗ
			|	РегистрНакопления.UnallocatedMemo.Остатки(&ПериодОстатки, Memo = &Memo) КАК UnallocatedMemoОстатки";
		
		Запрос.УстановитьПараметр("Memo", Invoice);
		Запрос.УстановитьПараметр("ПериодОстатки", Новый Граница(Дата, ВидГраницы.Включая));
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Количество() = 1 Тогда
			
			ВыборкаДетальныеЗаписи.Следующий();
			
			ДвижениеUnallocated = ДвиженияUnallocatedMemo.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеUnallocated, Invoice);
			ДвижениеUnallocated.Период = Дата;
			ДвижениеUnallocated.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеUnallocated.Memo = Invoice;
			ДвижениеUnallocated.Amount = ВыборкаДетальныеЗаписи.AmountОстаток;
			ДвижениеUnallocated.BaseAmount = ВыборкаДетальныеЗаписи.BaseAmountОстаток;
			
			ДвижениеBilledAR = ДвиженияBilledAR.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеBilledAR, Invoice);
			ДвижениеBilledAR.Период = Дата;
			ДвижениеBilledAR.ВидДвижения = ВидДвиженияНакопления.Приход;
			ДвижениеBilledAR.Invoice = Invoice;
			ДвижениеBilledAR.Amount = ВыборкаДетальныеЗаписи.AmountОстаток;
			ДвижениеBilledAR.BaseAmount = ВыборкаДетальныеЗаписи.BaseAmountОстаток;
			
		ИначеЕсли ВыборкаДетальныеЗаписи.Количество() > 1 Тогда
			
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Remains of this memo diverge in analytics."); //остатки в регистре по мемо на разной аналитике, такого быть не должно
			
		КонецЕсли;
		
		
		ДвижениеBilledAR = ДвиженияBilledAR.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеBilledAR, Invoice);
		Amount = CreditNotes.Итог("Amount");
		ДвижениеBilledAR.Период = Дата;
		ДвижениеBilledAR.ВидДвижения = ВидДвиженияНакопления.Приход;
		ДвижениеBilledAR.Invoice = Invoice;
		ДвижениеBilledAR.Amount = -Amount;
		ДвижениеBilledAR.BaseAmount = -Amount / ?(ЗначениеЗаполнено(СтруктураКурсаInvoice.Курс), СтруктураКурсаInvoice.Курс, 1);
		
		Для каждого СтрокаТЧ Из CreditNotes Цикл
			
			СтруктураКурсаCreditNote = РаботаСКурсамиВалют.ПолучитьВнутреннийКурсВалюты(СтрокаТЧ.Currency, Дата);
			ДвижениеBilledAR = ДвиженияBilledAR.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеBilledAR, СтрокаТЧ.CreditNote);
			ДвижениеBilledAR.Период = Дата;
			ДвижениеBilledAR.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеBilledAR.Invoice = СтрокаТЧ.CreditNote;
			ДвижениеBilledAR.Amount = -СтрокаТЧ.Amount;
			ДвижениеBilledAR.BaseAmount = -СтрокаТЧ.Amount / ?(ЗначениеЗаполнено(СтруктураКурсаCreditNote.Курс), СтруктураКурсаCreditNote.Курс, 1);
			
		КонецЦикла;
		
		
		ДвиженияBilledAR.Записывать = Истина;
		ДвиженияUnallocatedMemo.Записывать = Истина;
		
	ИначеЕсли ВидОперацииNoteAllocation = Перечисления.ВидыОперацийNoteAllocation.DebitMemo Тогда
		
		ДвиженияUnallocatedMemo = Движения.UnallocatedMemo;
		
		Если CreditNotes.Количество() = 1 Тогда
			
			Мемо2 = CreditNotes[0].CreditNote;
			
			Если ЗначениеЗаполнено(Мемо2) Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ
					|	UnallocatedMemoОстатки.AmountОстаток,
					|	UnallocatedMemoОстатки.BaseAmountОстаток
					|ИЗ
					|	РегистрНакопления.UnallocatedMemo.Остатки(&ПериодОстатков, Memo = &Memo1) КАК UnallocatedMemoОстатки
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	UnallocatedMemoОстатки.AmountОстаток,
					|	UnallocatedMemoОстатки.BaseAmountОстаток
					|ИЗ
					|	РегистрНакопления.UnallocatedMemo.Остатки(&ПериодОстатков, Memo = &Memo2) КАК UnallocatedMemoОстатки";
				
				Запрос.УстановитьПараметр("Memo1", Invoice);
				Запрос.УстановитьПараметр("Memo2", Мемо2);
				Запрос.УстановитьПараметр("ПериодОстатков", Новый Граница(Дата, ВидГраницы.Включая));
				
				МассивРезультатов = Запрос.ВыполнитьПакет();
				
				ТаблицаМемо1 = МассивРезультатов[0].Выгрузить();
				ТаблицаМемо2 = МассивРезультатов[1].Выгрузить();
				
				СуммаДляКорректировки = Мин(Макс(ТаблицаМемо1[0].AmountОстаток, -ТаблицаМемо1[0].AmountОстаток), Макс(ТаблицаМемо2[0].AmountОстаток, -ТаблицаМемо2[0].AmountОстаток));
				СуммаДляКорректировкиUSD = Мин(Макс(ТаблицаМемо1[0].BaseAmountОстаток, -ТаблицаМемо1[0].BaseAmountОстаток), Макс(ТаблицаМемо2[0].BaseAmountОстаток, -ТаблицаМемо2[0].BaseAmountОстаток));
				
				ДвижениеUnallocated = ДвиженияUnallocatedMemo.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеUnallocated, Invoice);
				ДвижениеUnallocated.Период = Дата;
				ДвижениеUnallocated.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеUnallocated.Memo = Invoice;
				Если ТаблицаМемо1[0].AmountОстаток > 0 Тогда
					ДвижениеUnallocated.Amount = -СуммаДляКорректировки;
					ДвижениеUnallocated.BaseAmount = -СуммаДляКорректировкиUSD;
				Иначе
					ДвижениеUnallocated.Amount = СуммаДляКорректировки;
					ДвижениеUnallocated.BaseAmount = СуммаДляКорректировкиUSD;
				КонецЕсли;
					
				ДвижениеUnallocated = ДвиженияUnallocatedMemo.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеUnallocated, Мемо2);
				ДвижениеUnallocated.Период = Дата;
				ДвижениеUnallocated.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеUnallocated.Memo = Мемо2;
				Если ТаблицаМемо2[0].AmountОстаток > 0 Тогда
					ДвижениеUnallocated.Amount = -СуммаДляКорректировки;
					ДвижениеUnallocated.BaseAmount = -СуммаДляКорректировкиUSD;
				Иначе
					ДвижениеUnallocated.Amount = СуммаДляКорректировки;
					ДвижениеUnallocated.BaseAmount = СуммаДляКорректировкиUSD;
				КонецЕсли;
				
				ДвиженияUnallocatedMemo.Записывать = Истина;
				
			Иначе
				 //ругаться, что не заполнен инвойс
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Select the memo.");
			КонецЕсли;
		Иначе
			//ругаться, что не одна строка
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("For this type of operation can be only one credit note.");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьНаличиеБалансовПоДокументам()
	
	НаличиеБаланса = НаличиеБалансаПоДокументу(Invoice);
	
	Для каждого СтрокаТЧ Из CreditNotes Цикл
		 НаличиеБаланса = НаличиеБаланса И НаличиеБалансаПоДокументу(СтрокаТЧ.CreditNote);
	КонецЦикла;
	
	Возврат НаличиеБаланса;
	
КонецФункции

Функция НаличиеБалансаПоДокументу(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	BilledARОстатки.Invoice КАК Invoice,
		|	BilledARОстатки.AmountОстаток КАК AmountОстаток
		|ИЗ
		|	РегистрНакопления.BilledAR.Остатки(&ПериодОстатков, Invoice = &Документ) КАК BilledARОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	UnallocatedMemoОстатки.Memo,
		|	UnallocatedMemoОстатки.AmountОстаток
		|ИЗ
		|	РегистрНакопления.UnallocatedMemo.Остатки(&ПериодОстатков, Memo = &Документ) КАК UnallocatedMemoОстатки";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("ПериодОстатков", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи.Количество() > 0;
	
КонецФункции

#КонецЕсли