
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("СтарыеЗначенияКлючей", ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Client, Account"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("СтарыеЗначенияКлючей") Тогда
		
		СтарыеЗначенияКлючей = ДополнительныеСвойства.СтарыеЗначенияКлючей;
		Если СтарыеЗначенияКлючей.Client <> Client ИЛИ СтарыеЗначенияКлючей.Account <> Account Тогда
			
			НЗ = РегистрыСведений.КлючиРучныхКорректировок.СоздатьНаборЗаписей();
			НЗ.Отбор.Client.Установить(СтарыеЗначенияКлючей.Client);
			НЗ.Отбор.Account.Установить(СтарыеЗначенияКлючей.Account);
			НЗ.Записать(Истина);
			
			НЗ.Отбор.Client.Установить(Client);
			НЗ.Отбор.Account.Установить(Account);
			ЗаписьНабора = НЗ.Добавить();
			ЗаписьНабора.Client = Client;
			ЗаписьНабора.Account = Account;
			ЗаписьНабора.РучнаяКорректировка = Ссылка;
			НЗ.Записать(Истина);
			
		КонецЕсли;
		
	Иначе
		
		НЗ = РегистрыСведений.КлючиРучныхКорректировок.СоздатьНаборЗаписей();
		НЗ.Отбор.Client.Установить(Client);
		НЗ.Отбор.Account.Установить(Account);
		ЗаписьНабора = НЗ.Добавить();
		ЗаписьНабора.Client = Client;
		ЗаписьНабора.Account = Account;
		ЗаписьНабора.РучнаяКорректировка = Ссылка;
		НЗ.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	НЗ = РегистрыСведений.КлючиРучныхКорректировок.СоздатьНаборЗаписей();
	НЗ.Отбор.Client.Установить(Client);
	НЗ.Отбор.Account.Установить(Account);
	НЗ.Записать(Истина);
	
КонецПроцедуры

