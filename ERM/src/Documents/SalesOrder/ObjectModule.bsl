#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриЗаписи(Отказ)
	
	НЗ_КлючиНотификаций = РегистрыСведений.ИдентификаторыДляНотификацийSO.СоздатьНаборЗаписей();
	НЗ_КлючиНотификаций.Отбор.SalesOrder.Установить(Ссылка);
	
	ЗаписьКлючейНотификаций = НЗ_КлючиНотификаций.Добавить();
	ЗаписьКлючейНотификаций.Source = Source;
	ЗаписьКлючейНотификаций.SalesOrder = Ссылка;
	
	Если Source = Перечисления.ТипыСоответствий.Lawson Тогда
		ЗаписьКлючейНотификаций.Идентификатор1 = AU;
	ИначеЕсли Source = Перечисления.ТипыСоответствий.OracleMI Тогда
		ЗаписьКлючейНотификаций.Идентификатор1 = Location;
		ЗаписьКлючейНотификаций.Идентификатор2 = Client;
	ИначеЕсли Source = Перечисления.ТипыСоответствий.OracleSmith Тогда
		ЗаписьКлючейНотификаций.Идентификатор1 = Location;
	ИначеЕсли Source = Перечисления.ТипыСоответствий.HOBs Тогда
		ЗаписьКлючейНотификаций.Идентификатор1 = Company;
		ЗаписьКлючейНотификаций.Идентификатор2 = CREW;
	ИначеЕсли Source = Перечисления.ТипыСоответствий.Radius Тогда
		ЗаписьКлючейНотификаций.Идентификатор1 = Company;
	ИначеЕсли Source = Перечисления.ТипыСоответствий.HOBsKZU Тогда
		ЗаписьКлючейНотификаций.Идентификатор1 = Company;
	КонецЕсли;
	
	НЗ_КлючиНотификаций.Записать();
	
	НЗ_SalesOrderResponsibles = РегистрыСведений.SalesOrderResponsibles.СоздатьНаборЗаписей();
	НЗ_SalesOrderResponsibles.Отбор.SalesOrder.Установить(Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПолучателиУведомленийUnbilled.Получатель
		|ИЗ
		|	РегистрСведений.ПолучателиУведомленийUnbilled КАК ПолучателиУведомленийUnbilled
		|ГДЕ
		|	ПолучателиУведомленийUnbilled.Source = &Source
		|	И ПолучателиУведомленийUnbilled.Идентификатор1 = &Идентификатор1
		|	И ПолучателиУведомленийUnbilled.Идентификатор2 = &Идентификатор2
		|	И ПолучателиУведомленийUnbilled.Получатель.ResponsibleAR
		|	И ПолучателиУведомленийUnbilled.Уровень = ЗНАЧЕНИЕ(Справочник.EscalationLevels.Level1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПолучателиУведомленийUnbilled.Получатель
		|ИЗ
		|	РегистрСведений.ПолучателиУведомленийUnbilled КАК ПолучателиУведомленийUnbilled
		|ГДЕ
		|	ПолучателиУведомленийUnbilled.Source = &Source
		|	И ПолучателиУведомленийUnbilled.Идентификатор1 = &Идентификатор1
		|	И (ПолучателиУведомленийUnbilled.Идентификатор2 = НЕОПРЕДЕЛЕНО
		|			ИЛИ ПолучателиУведомленийUnbilled.Идентификатор2 = """"
		|			ИЛИ ПолучателиУведомленийUnbilled.Идентификатор2 = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ИЛИ ПолучателиУведомленийUnbilled.Идентификатор2 = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
		|	И ПолучателиУведомленийUnbilled.Получатель.ResponsibleAR
		|	И ПолучателиУведомленийUnbilled.Уровень = ЗНАЧЕНИЕ(Справочник.EscalationLevels.Level1)";
	
	Запрос.УстановитьПараметр("Source", ЗаписьКлючейНотификаций.Source);
	Запрос.УстановитьПараметр("Идентификатор1", ЗаписьКлючейНотификаций.Идентификатор1);
	Запрос.УстановитьПараметр("Идентификатор2", ЗаписьКлючейНотификаций.Идентификатор2);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если НЕ МассивРезультатов[0].Пустой() Тогда
		ВыборкаДетальныеЗаписи = МассивРезультатов[0].Выбрать();
	Иначе
		ВыборкаДетальныеЗаписи = МассивРезультатов[1].Выбрать();
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗаписьНабора = НЗ_SalesOrderResponsibles.Добавить();
		ЗаписьНабора.SalesOrder = Ссылка;
		ЗаписьНабора.ResponsibleAR = ВыборкаДетальныеЗаписи.Получатель;
		
	КонецЦикла;
	
	НЗ_SalesOrderResponsibles.Записать();
	
	Если Source <> Перечисления.ТипыСоответствий.Lawson И Source <> Перечисления.ТипыСоответствий.Radius И Source <> Перечисления.ТипыСоответствий.OracleMI Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СтарыеЗначенияКлючей") Тогда
		
		СтарыеЗначенияКлючей = ДополнительныеСвойства.СтарыеЗначенияКлючей;
		Если СтарыеЗначенияКлючей.ArInvoice <> ArInvoice ИЛИ СтарыеЗначенияКлючей.Company <> Company Тогда
			
			НЗ = РегистрыСведений.КлючиSalesOrders.СоздатьНаборЗаписей();
			НЗ.Отбор.ArInvoice.Установить(СтарыеЗначенияКлючей.ArInvoice);
			НЗ.Отбор.Company.Установить(СтарыеЗначенияКлючей.Company);
			НЗ.Отбор.Source.Установить(СтарыеЗначенияКлючей.Source);
			НЗ.Записать(Истина);
			
			НЗ.Отбор.ArInvoice.Установить(ArInvoice);
			НЗ.Отбор.Company.Установить(Company);
			ЗаписьНабора = НЗ.Добавить();
			ЗаписьНабора.ArInvoice = ArInvoice;
			ЗаписьНабора.Company = Company;
			ЗаписьНабора.Source = Source;
			ЗаписьНабора.SalesOrder = Ссылка;
			НЗ.Записать(Истина);
			
		КонецЕсли;
		
	Иначе
		
		НЗ = РегистрыСведений.КлючиSalesOrders.СоздатьНаборЗаписей();
		НЗ.Отбор.ArInvoice.Установить(ArInvoice);
		НЗ.Отбор.Company.Установить(Company);
		НЗ.Отбор.Source.Установить(Source);
		ЗаписьНабора = НЗ.Добавить();
		ЗаписьНабора.ArInvoice = ArInvoice;
		ЗаписьНабора.Company = Company;
		ЗаписьНабора.Source = Source;
		ЗаписьНабора.SalesOrder = Ссылка;
		НЗ.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Source <> Перечисления.ТипыСоответствий.Lawson И Source <> Перечисления.ТипыСоответствий.Radius И Source <> Перечисления.ТипыСоответствий.OracleMI Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("СтарыеЗначенияКлючей", ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ArInvoice, Company, Source"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если Source <> Перечисления.ТипыСоответствий.Lawson Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.КлючиSalesOrders.СоздатьНаборЗаписей();
	НЗ.Отбор.ArInvoice.Установить(ArInvoice);
	НЗ.Отбор.Company.Установить(Company);
	НЗ.Отбор.Source.Установить(Source);
	НЗ.Записать(Истина);
	
КонецПроцедуры

#КонецЕсли
